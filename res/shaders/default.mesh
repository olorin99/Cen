#version 460

#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_buffer_reference2 : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_shader_explicit_arithmetic_types : enable
#extension GL_EXT_mesh_shader : enable

#include "shaders/cen.glsl"

layout (location = 0) out VsOut {
    flat uint drawId;
    flat uint meshletId;
} meshOut[];

layout (push_constant) uniform Push {
    GlobalDataRef globalDataRef;
    MeshletBuffer meshletBuffer;
    MeshletInstanceBuffer meshletInstanceBuffer;
    VertexBuffer vertexBuffer;
    IndexBuffer indexBuffer;
    PrimitiveBuffer primitiveBuffer;
    TransformsBuffer transformsBuffer;
    CameraBuffer cameraBuffer;
};

layout (local_size_x = 64) in;
layout (triangles, max_vertices = 64, max_primitives = 64) out;
void main() {

    uint threadIndex = gl_LocalInvocationIndex;

    GPUCamera camera = cameraBuffer.camera;

    uint meshletIndex = gl_WorkGroupID.x + gl_NumWorkGroups.x * gl_WorkGroupID.y;
    if (meshletIndex >= meshletInstanceBuffer.count) {
        SetMeshOutputsEXT(0, 0);
        return;
    }
    MeshletInstance instance = meshletInstanceBuffer.instances[meshletIndex];
    Meshlet meshlet = meshletBuffer.meshlets[instance.meshletId];

    SetMeshOutputsEXT(meshlet.indexCount, meshlet.primitiveCount);

    if (threadIndex == 0) {
        atomicAdd(globalDataRef.globalData.feedbackInfoRef.info.trianglesDrawn, meshlet.primitiveCount);
    }

    if (threadIndex < meshlet.indexCount) {
        uint index = indexBuffer.indices[meshlet.indexOffset + threadIndex] + meshlet.vertexOffset;
        Vertex vertex = vertexBuffer.vertices[index];
        vec4 fragPos = transformsBuffer.transforms[instance.meshId] * vec4(vertex.position, 1.0);

        meshOut[threadIndex].drawId = instance.meshId;
        meshOut[threadIndex].meshletId = instance.meshletId;

        gl_MeshVerticesEXT[threadIndex].gl_Position = camera.projection * camera.view * fragPos;
    }

    if (threadIndex < meshlet.primitiveCount) {
        uint a = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 0];
        uint b = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 1];
        uint c = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 2];

        gl_PrimitiveTriangleIndicesEXT[threadIndex] = uvec3(a, b, c);
        gl_MeshPrimitivesEXT[threadIndex].gl_PrimitiveID = int(threadIndex);
    }
}