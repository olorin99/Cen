#version 460

#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_buffer_reference2 : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_shader_explicit_arithmetic_types : enable

#include "shaders/cen.glsl"

struct DrawIndirectCommand {
    uint vertexCount;
    uint instanceCount;
    uint firstVertex;
    uint firstInstance;
};
layout (scalar, buffer_reference, buffer_reference_align = 4) writeonly buffer DrawCommandBuffer {
    uint drawCount;
    DrawIndirectCommand commands[];
};

layout (push_constant) uniform Push {
    GlobalDataRef globalDataRef;
    MeshletBuffer meshletBuffer;
    MeshletInstanceBuffer meshletInstanceBuffer;
    PrimitiveBuffer primitiveBuffer;
    MeshletIndexBuffer meshletIndexBuffer;
    DrawCommandBuffer drawCommandBuffer;
};

shared uint sharedIndexOffset;

layout (local_size_x = 64) in;
void main() {

    if (gl_GlobalInvocationID.x == 0) {
        meshletIndexBuffer.indexCount = 0;
        drawCommandBuffer.drawCount = 0;
    }
    barrier();

    uint threadIndex = gl_LocalInvocationIndex;
    uint meshletIndex = gl_WorkGroupID.x + gl_NumWorkGroups.x * gl_WorkGroupID.y;
    if (meshletIndex >= meshletInstanceBuffer.count) {
        return;
    }

    MeshletInstance instance = meshletInstanceBuffer.instances[meshletIndex];
    Meshlet meshlet = meshletBuffer.meshlets[instance.meshletId];

    if (gl_LocalInvocationIndex == 0) {
        sharedIndexOffset = atomicAdd(meshletIndexBuffer.indexCount, meshlet.primitiveCount * 3);
        uint drawIndex = atomicAdd(drawCommandBuffer.drawCount, 1);
        DrawIndirectCommand drawCommand;
        drawCommand.vertexCount = meshlet.primitiveCount * 3;
        drawCommand.firstVertex = sharedIndexOffset;
        drawCommand.instanceCount = 1;
        drawCommand.firstInstance = 0;
        drawCommandBuffer.commands[drawIndex] = drawCommand;
    }
    barrier();

    if (threadIndex < meshlet.primitiveCount) {
        uint a = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 0];
        uint b = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 1];
        uint c = primitiveBuffer.primitives[meshlet.primitiveOffset + threadIndex * 3 + 2];

        uint indexOffset = sharedIndexOffset + threadIndex * 3;
        uint meshletId = gl_WorkGroupID.x;
        meshletIndexBuffer.indices[indexOffset + 0] = setMeshletID(meshletId) | setPrimitiveID(a);
        meshletIndexBuffer.indices[indexOffset + 1] = setMeshletID(meshletId) | setPrimitiveID(b);
        meshletIndexBuffer.indices[indexOffset + 2] = setMeshletID(meshletId) | setPrimitiveID(c);
    }

}
